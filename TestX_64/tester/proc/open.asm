;----------------------------------------------------------------------------------------------------
;   Web Server + HTML Tester x64: ver. 2.75
;   BASE: Open Source DataBase
;   (c) Kyiv, Ruslan FoXx
;   01 July 2024
;----------------------------------------------------------------------------------------------------
;
;       * * *  Write Index Attributes
;
;----------------------------------------------------------------------------------------------------
proc SetIndexAttribute
;----------------------------------------------------------------------------------------------------
    mov RDI, Time
;   mov BL,  Attribute
;----------------------------------------------------------------------------------------------------
    mov AH, BL
    mov [RDI], AX
;----------------------------------------------------------------------------------------------------
    xor RAX, RAX
    mov AL,  2    ;   WORD
    mov R8,  RAX
;----------------------------------------------------------------------------------------------------
;   xor EAX, EAX
    mov AL,  INDEX_HEADER_SIZE
    mov EBX, [IndexDataBase.fields]
    sub EBX, [IndexDataBase.session]
    mul EBX
    add EAX, INDEX_HEADER.count + 2
;----------------------------------------------------------------------------------------------------
    param 4, RAX
;   param 3, 1
    param 2, RDI  ;  Time
    param 1, [IndexBasePath]
;----------------------------------------------------------------------------------------------------
    call WriteToPosition
;----------------------------------------------------------------------------------------------------
;   xor EAX, EAX
    mov AL,  BASE_INDEX + ERR_WRITE
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Open IndexBase  * * *
;
;----------------------------------------------------------------------------------------------------
proc OpenIndexBase
;----------------------------------------------------------------------------------------------------
    test EBX, EBX
         jz jmpEnd@OpenIndexBase
;----------------------------------------------------------------------------------------------------
         mov [IndexDataBase.session], EBX
;----------------------------------------------------------------------------------------------------
;       * * *  Get BasePath
;----------------------------------------------------------------------------------------------------
    param 1, [IndexBasePath]
    call ReadToBuffer
;----------------------------------------------------------------------------------------------------
    xor RBX, RBX
    mov BL,  INDEX_HEADER_SIZE
;----------------------------------------------------------------------------------------------------
    cmp ECX, EBX
        jbe jmpEnd@OpenIndexBase
;----------------------------------------------------------------------------------------------------
;       * * *  Get Index
;----------------------------------------------------------------------------------------------------
        mov RSI, [pReadBuffer]
        mov RDI, IndexDataBase
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
        lodsw
        stosd
        mov R8, RAX
;       mov [IndexDataBase.fields], EAX
;----------------------------------------------------------------------------------------------------
;       mov EBX, INDEX_HEADER_SIZE
        mul EBX
;----------------------------------------------------------------------------------------------------
        cmp EAX, ECX
            jae jmpEnd@OpenIndexBase
;----------------------------------------------------------------------------------------------------
            mov R9, RSI
            add R9, RAX       ;   text
;----------------------------------------------------------------------------------------------------
            mov RAX, R8
;           sub EAX, [StoreDataBase.session]
            sub EAX, [RDI]
                js jmpEnd@OpenIndexBase
;----------------------------------------------------------------------------------------------------
;               mov EBX, INDEX_HEADER_SIZE
                mul EBX
                add RSI, RAX  ;   index
                add RDI, 4
;----------------------------------------------------------------------------------------------------
                xor RAX, RAX
                lodsw
                stosd
;               mov [IndexDataBase.group], EAX
;----------------------------------------------------------------------------------------------------
                movsd
;               mov [IndexDataBase.date], EAX
;----------------------------------------------------------------------------------------------------
;               xor EAX, EAX
                lodsb
                stosd
;               mov [IndexDataBase.count], EAX
;----------------------------------------------------------------------------------------------------
;               xor EAX, EAX
                lodsb
                stosd
;               mov [IndexDataBase.attribute], EAX
;----------------------------------------------------------------------------------------------------
                lodsw
                add RAX, R9
                mov RBX, RAX       ;      for TestCaption
                mov [RDI], RAX
;               mov [IndexDataBase.testname], RSI
;----------------------------------------------------------------------------------------------------
                xor RAX, RAX
                ret
;----------------------------------------------------------------------------------------------------
jmpEnd@OpenIndexBase:
;   xor RAX, RAX
    mov AL,  BASE_INDEX + ERR_READ
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Open UserBase  * * *
;
;----------------------------------------------------------------------------------------------------
proc OpenUserBase
;----------------------------------------------------------------------------------------------------
    mov RDI, [GroupBasePath.name]
;   mov EBX, User
    call IndexToStr
;----------------------------------------------------------------------------------------------------
;       * * *  Read Test
;----------------------------------------------------------------------------------------------------
    param 1, [GroupBasePath.path]
    call ReadToBuffer
;----------------------------------------------------------------------------------------------------
    cmp ECX, GROUP_HEAD_SIZE
        jbe jmpEnd@OpenUserBase
;----------------------------------------------------------------------------------------------------
        mov RSI, [pReadBuffer]
        mov RDI, UserDataBase
;----------------------------------------------------------------------------------------------------
        movsd
;       mov [UserDataBase.date], EAX
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
        lodsb
        stosd
        mov RBX, RAX
;       mov [UserDataBase.count], EAX
;----------------------------------------------------------------------------------------------------
        mov RAX, RSI
        stosq
;       mov [UserDataBase.index], RSI
;----------------------------------------------------------------------------------------------------
        shl EBX, 2
        cmp EBX, ECX
            jae jmpEnd@OpenUserBase
;----------------------------------------------------------------------------------------------------
        add RBX, RAX
        mov [RDI], RBX     ;     for UserCaption
;       mov [UserDataBase.user], RSI
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
        ret
;----------------------------------------------------------------------------------------------------
jmpEnd@OpenUserBase:
;   xor RAX, RAX
    mov AL,  BASE_GROUP + ERR_READ
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Open TestBase * * *
;
;----------------------------------------------------------------------------------------------------
proc OpenTestBase
;----------------------------------------------------------------------------------------------------
;       * * *  Set PathFolder
;----------------------------------------------------------------------------------------------------
;   mov RSI, [Name]
    mov RSI, RBX
    mov RDX, [TestBasePath.name]
    mov RDI, RDX
    CopyString
;----------------------------------------------------------------------------------------------------
    mov RAX, RDI
    sub RAX, RDX
    mov [TestDataBase.pathsize], EAX
;----------------------------------------------------------------------------------------------------
    mov AL, '.'
    stosb
;----------------------------------------------------------------------------------------------------
    mov EAX, EXT_TEST
    stosd

;    invoke MessageBox, HWND_DESKTOP, [TestBasePath.path], szServiceName, MB_OK
;----------------------------------------------------------------------------------------------------
;       * * *  Read Test
;----------------------------------------------------------------------------------------------------
    param 1, [TestBasePath.path]
    call ReadToBuffer
;----------------------------------------------------------------------------------------------------
    cmp ECX, TEST_HEADER_SIZE
        jbe jmpEnd@OpenTestBase
;----------------------------------------------------------------------------------------------------
;       * * *  TestHeader
;----------------------------------------------------------------------------------------------------
        mov RSI, [pReadBuffer]
        mov RDI, TestDataBase
;----------------------------------------------------------------------------------------------------
        movsd
;       mov [TestDataBase.date], EAX
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
        lodsw
        stosd
        mov EBX, EAX 
;       mov [TestDataBase.questions], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsb
        stosd
        mov EDX, EAX
;       mov [TestDataBase.answers], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsw
        stosd
;       mov [TestDataBase.tests], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsw
        stosd
;       mov [TestDataBase.time], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsb
        stosd
;       mov [TestDataBase.level], EAX
;----------------------------------------------------------------------------------------------------
        mov EAX, EDX
        inc EAX
        shl EAX, 2
        add EAX, 2
;       stosd
;       stosd
        stosq  ; 4+4
;       mov [TestDataBase.fieldsize], EAX
;----------------------------------------------------------------------------------------------------
        mul EBX
        mov EBX, EAX 
;----------------------------------------------------------------------------------------------------
        mov RAX, RSI
        stosq
;       mov [TestDataBase.scale], RSI
;----------------------------------------------------------------------------------------------------
        add EAX, TEST_SCALE_COUNT * 2
        stosq
;       mov [TestDataBase.index], RSI
;----------------------------------------------------------------------------------------------------
        add EAX, EBX
;       mov RBX, RAX     ;       for TestCaption
        mov [RDI], RAX
;       mov [TestDataBase.text], RSI
;----------------------------------------------------------------------------------------------------
        sub RAX, RSI
        cmp EAX, ECX
            jae jmpEnd@OpenTestBase
;----------------------------------------------------------------------------------------------------
            xor RAX, RAX
            ret
;----------------------------------------------------------------------------------------------------
jmpEnd@OpenTestBase:
;   xor RAX, RAX
    mov AL,  BASE_TEST + ERR_READ
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Open TableBase  * * *
;
;----------------------------------------------------------------------------------------------------
proc OpenTableBase
;----------------------------------------------------------------------------------------------------
;       * * *  Set TablePath
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBasePath.table]
;   mov RSI, Table
;----------------------------------------------------------------------------------------------------
    movsq    ;    TABLE_NAME_LENGTH
    movsw
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBasePath.session]
;   mov EBX, [IndexDataBase.session]
    call IndexToStr
;----------------------------------------------------------------------------------------------------
;       * * *  Read Test
;----------------------------------------------------------------------------------------------------
    param 1, [TableBasePath.path]
    call ReadToBuffer
;----------------------------------------------------------------------------------------------------
    cmp ECX, TABLE_HEADER_SIZE
        jbe jmpEnd@OpenTableBase
;----------------------------------------------------------------------------------------------------
        mov RSI, [pReadBuffer]
        mov RDI, TableDataBase.session
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
;       inc EAX
;       stosd
;       mov [TableDataBase.count], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsw
        stosd
;       mov [TableDataBase.session], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsw
        stosd
;       mov [TableDataBase.group], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsw
        stosd
;       mov [TableDataBase.time], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsw
        stosd
        mov RBX, RAX
;       mov [TableDataBase.tests], EAX
;----------------------------------------------------------------------------------------------------
;       xor EAX, EAX
        lodsb
        stosd
        mov EDX, EAX
;       mov [TableDataBase.items], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsb
        stosd
;       mov [TableDataBase.user], EAX
;----------------------------------------------------------------------------------------------------
        movsq
;       mov [TableDataBase.start], EAX
;       mov [TableDataBase.close], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsw
        stosd
;       mov [TableDataBase.total], EAX
;----------------------------------------------------------------------------------------------------
        xor EAX, EAX
        lodsb
        stosd
;       mov [TableDataBase.score], EAX
;----------------------------------------------------------------------------------------------------
        mov EAX, EDX
        add AL,  2
        stosd
;       mov [TableDataBase.fieldsize], EAX
;----------------------------------------------------------------------------------------------------
        mul EBX
        mov EDX, EAX
        shl EBX, 1
        add EAX, EBX
        mov EBX, EAX
;       stosd
;       stosd
        stosq  ; 4+4
;       mov [TableDataBase.tablesize], ESI
;----------------------------------------------------------------------------------------------------
        cmp EAX, ECX
            jae jmpEnd@OpenTableBase
;----------------------------------------------------------------------------------------------------
        mov RAX, RSI
        stosq
;       mov [TableDataBase.index], RSI
;----------------------------------------------------------------------------------------------------
        add RAX, RDX
        stosq
;       mov [TableDataBase.data], RSI
;----------------------------------------------------------------------------------------------------;
        add RBX, RSI
;       mov RAX, RBX
        mov [RDI], RBX    ;           for TestBaseName
;       mov [TableDataBase.testname], RSI
;----------------------------------------------------------------------------------------------------
        xor RAX, RAX
        ret
;----------------------------------------------------------------------------------------------------
jmpEnd@OpenTableBase:
;   xor RAX, RAX
    mov AL,  BASE_TABLE + ERR_READ
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Open StoreBase  * * *
;
;----------------------------------------------------------------------------------------------------
proc OpenStoreBase
;----------------------------------------------------------------------------------------------------
    test EDX, EDX
         jz jmpEnd@OpenStoreBase
;----------------------------------------------------------------------------------------------------
;        mov [TableDataBase.count], EDX
         mov [TableDataBase], EDX
;----------------------------------------------------------------------------------------------------
;       * * *  Set StorePath
;----------------------------------------------------------------------------------------------------
    mov RDI, [StoreBasePath.name]
;   mov RBX, [IndexDataBase.session]
    call IndexToStr
;----------------------------------------------------------------------------------------------------
;       * * *  Set TablePath
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBasePath.session]
    mov RSI, [StoreBasePath.name]
    movsd
    movsb
;----------------------------------------------------------------------------------------------------
;       * * *  Read Test
;----------------------------------------------------------------------------------------------------
    param 1, [StoreBasePath.path]
    call ReadToBuffer
;----------------------------------------------------------------------------------------------------
    cmp ECX, STORE_HEADER_SIZE
        jbe jmpEnd@OpenStoreBase
;----------------------------------------------------------------------------------------------------
        mov RSI, [pReadBuffer]
        mov RDI, TableDataBase
;----------------------------------------------------------------------------------------------------
        xor RAX,  RAX
        mov R10,  RAX
;       mov R10d, [TableDataBase.count]
        mov R10d, [RDI]    ;    index
        dec R10d
;----------------------------------------------------------------------------------------------------
;       xor RAX, RAX
        lodsb
        cmp EAX, R10d
            jb jmpEnd@OpenStoreBase
;----------------------------------------------------------------------------------------------------
            stosd
            mov R11d, EAX
;           mov [TableDataBase.count], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsw
            stosd
;           mov [TableDataBase.session], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsw
            stosd
;           mov [TableDataBase.group], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsw
            stosd
;           mov [TableDataBase.time], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsw
            stosd
            mov RBX, RAX
;           mov [TableDataBase.tests], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsb
            stosd
;           mov [TableDataBase.items], EAX
;----------------------------------------------------------------------------------------------------
            add AL, 2
            mov R8, RAX
;----------------------------------------------------------------------------------------------------
            mul EBX
            mov R9, RAX
;----------------------------------------------------------------------------------------------------
            shl EBX, 1
            add EAX, EBX
            add EAX, TABLE_HEADER_DATA
            mov EBX, EAX
;----------------------------------------------------------------------------------------------------
            mul R10d
            mov R12, RSI
            add RSI, RAX
;----------------------------------------------------------------------------------------------------
            xor RAX, RAX
            lodsb
            stosd
;           mov [TableDataBase.user], EAX
;----------------------------------------------------------------------------------------------------
            movsq
;           mov [TableDataBase.close], EAX
;           mov [TableDataBase.start], EAX
;----------------------------------------------------------------------------------------------------
;           xor EAX, EAX
            lodsw
            stosd
;           mov [TableDataBase.total], EAX
;----------------------------------------------------------------------------------------------------
            xor EAX, EAX
            lodsb
            stosd
;           mov [TableDataBase.score], EAX
;----------------------------------------------------------------------------------------------------
            mov EAX, R8d
            stosd
;           mov [TableDataBase.fieldsize], EAX
;----------------------------------------------------------------------------------------------------
            mov EAX, EBX
;           stosd
;           stosd
            stosq  ; 4+4
;           mov [TableDataBase.tablesize], EAX
;----------------------------------------------------------------------------------------------------
            mov RAX, RSI
            stosq
;           mov [TableDataBase.index], RSI
;----------------------------------------------------------------------------------------------------
            add RAX, R9
            stosq
;           mov [TableDataBase.data], RSI
;----------------------------------------------------------------------------------------------------;
            mov EAX, R11d   ;   count
            mul EBX
            cmp EAX, ECX
                jae jmpEnd@OpenTableBase
;----------------------------------------------------------------------------------------------------;
            add RAX, R12
            mov RBX, RAX   ;   for TestBaseName
            stosq
;           mov [TableDataBase.testname], RSI
;----------------------------------------------------------------------------------------------------
;           mov RAX, R12
            mov [RDI], R12
;           mov [TableDataBase.table], RSI
;----------------------------------------------------------------------------------------------------
            xor RAX, RAX
            ret
;----------------------------------------------------------------------------------------------------
jmpEnd@OpenStoreBase:
;   xor RAX, RAX
    mov AL,  BASE_STORE + ERR_READ
    ret 
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *  Delete TableBase + Directory  * * *
;
;----------------------------------------------------------------------------------------------------
proc DeleteTableBase
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBasePath.session]
;   mov EBX, [IndexDataBase.session]
    call IndexToStr
;----------------------------------------------------------------------------------------------------
;       * * *  Set TablePath
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBaseScan.dir]
    mov RSI, [TableBasePath.session]
    movsd
    movsb
;----------------------------------------------------------------------------------------------------
    xor RAX, RAX
    mov [CountFiles], RAX
;----------------------------------------------------------------------------------------------------
    mov AL,  32    ;    for 7 + 8
    sub RSP, RAX
;----------------------------------------------------------------------------------------------------
;       * * *  Get First Table
;----------------------------------------------------------------------------------------------------
    param 2, FindFileData 
    param 1, [TableBaseScan.path]
;----------------------------------------------------------------------------------------------------
    call [FindFirstFile]
;----------------------------------------------------------------------------------------------------
    cmp EAX, INVALID_HANDLE_VALUE
        je jmpEnd@DeleteTableBase
;----------------------------------------------------------------------------------------------------
        mov [hFind], EAX
;----------------------------------------------------------------------------------------------------
;       * * *  Scan Folder
;----------------------------------------------------------------------------------------------------
jmpFileFind@DeleteTableBase:
;----------------------------------------------------------------------------------------------------
        mov RDI, [TableBasePath.table]
        mov RSI, FindFileData.cFileName
;----------------------------------------------------------------------------------------------------
        movsq    ;    TABLE_NAME_LENGTH
        movsw
;----------------------------------------------------------------------------------------------------
        param 1, mrTablePath
        call [DeleteFile]
;----------------------------------------------------------------------------------------------------
        test EAX, EAX
             jz jmpFileNext@DeleteTableBase
             inc [CountFiles]
;----------------------------------------------------------------------------------------------------
jmpFileNext@DeleteTableBase:
        param 2, FindFileData 
        param 1, [hFind]
;----------------------------------------------------------------------------------------------------
        call [FindNextFile]
;----------------------------------------------------------------------------------------------------
        test EAX, EAX
             jnz jmpFileFind@DeleteTableBase
;----------------------------------------------------------------------------------------------------
;jmpClose@DeleteTableBase:
    param 1, [hFind]
    call [FindClose]
;----------------------------------------------------------------------------------------------------
;       * * *  Delete Folder
;----------------------------------------------------------------------------------------------------
    mov RDI, [TableBaseScan.name]
    xor EAX, EAX
    mov [RDI], AL
;----------------------------------------------------------------------------------------------------
    param 1, mrTablePath
    call [RemoveDirectory]
;----------------------------------------------------------------------------------------------------
    test EAX, EAX
         jz jmpEnd@DeleteTableBase
;----------------------------------------------------------------------------------------------------
jmpEnd@DeleteTableBase:
    mov RCX, [CountFiles]
;----------------------------------------------------------------------------------------------------
    xor RAX, RAX
    mov AL,  32
    add RSP, RAX
    ret
endp
;----------------------------------------------------------------------------------------------------
;
;       * * *   END  * * *
;
;----------------------------------------------------------------------------------------------------

